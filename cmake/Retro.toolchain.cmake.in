# Required input variables:
#
# RETRO_ABI    - Target triplet
#
# Optional input variables:
#
# RETRO_ROOT   - Overrides the root directory of the toolchain
# RETRO_CARBON - Enables Carbon for PowerPC Mac
#
# Output variables:
#
# RETRO        - ON when using this toolchain
# RETRO_PALMOS - ON when building for any Palm OS
# RETRO_MAC    - ON when building for any Mac OS
# RETRO_68K    - ON when building for 68K Mac
# RETRO_PPC    - ON when building for non-Carbon PowerPC Mac
# RETRO_CARBON - ON when building for Carbon PowerPC Mac

set(CMAKE_SYSTEM_NAME Retro)
set(CMAKE_SYSTEM_VERSION 2)

set(RETRO_ROOT "${CMAKE_CURRENT_LIST_DIR}/../../" CACHE PATH "Path to Retro68 toolchain")
set(RETRO_ABI "" CACHE STRING "ABI target")
set(RETRO_CARBON OFF CACHE BOOL "Enable Carbon API")
list(APPEND CMAKE_TRY_COMPILE_PLATFORM_VARIABLES RETRO_ROOT RETRO_ABI RETRO_CARBON)

set(CMAKE_INSTALL_PREFIX "${RETRO_ROOT}/${RETRO_ABI}" CACHE PATH "Installation prefix")
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

set(RETRO_SUPPORTED_ABIS "@RETRO_SUPPORTED_ABIS@")
list(FIND RETRO_SUPPORTED_ABIS "${RETRO_ABI}" i)
if(i EQUAL -1)
    string(REPLACE ";" "\", \"" abis "${RETRO_SUPPORTED_ABIS}")
    message(FATAL_ERROR
        "Specified RETRO_ABI \"${RETRO_ABI}\" is not supported. Possible values"
        " are: \"${abis}\"")
endif()

set(CMAKE_C_COMPILER "${RETRO_ROOT}/bin/${RETRO_ABI}-gcc${CMAKE_EXECUTABLE_SUFFIX}")

if(RETRO_LEGACY_TOOLCHAIN)
    message(WARNING "You are using a legacy toolchain file. Please use"
        " ${CMAKE_COMMAND} \"-DCMAKE_TOOLCHAIN_FILE=${CMAKE_CURRENT_LIST_FILE}\""
        " -DRETRO_ABI=${RETRO_ABI} -DRETRO_CARBON=${RETRO_CARBON}` instead and"
        " call `find_program` and `find_library` instead of using predefined"
        " globals from the toolchain.")

    if(RETRO68_ROOT)
        message(WARNING "Use RETRO_ROOT instead of RETRO68_ROOT.")
        set(RETRO_ROOT "${RETRO68_ROOT}")
    endif()

    if(RETRO_ABI STREQUAL "m68k-apple-macos")
        set(CMAKE_SYSTEM_NAME Retro68)
    elseif(RETRO_ABI STREQUAL "powerpc-apple-macos")
        set(MAKE_PEF "${RETRO_ROOT}/bin/MakePEF")
        set(MAKE_IMPORT "${RETRO_ROOT}/bin/MakeImport")
        if(RETRO_CARBON)
            set(CMAKE_SYSTEM_NAME RetroCarbon)
        else()
            set(CMAKE_SYSTEM_NAME RetroPPC)
        endif()
    endif()

    set(CMAKE_SYSTEM_PREFIX_PATH "${RETRO_ROOT}/${RETRO_ABI}")
    set(REZ "${RETRO_ROOT}/bin/Rez")
    set(REZ_INCLUDE_PATH "${RETRO_ROOT}/share/Retro68/RIncludes;${RETRO_ROOT}/share/Retro68/MacSDK/RIncludes")
    set(REZ_TEMPLATES_PATH ${REZ_INCLUDE_PATH})
    include(add_application)
else()
    set(CMAKE_SYSROOT "${RETRO_ROOT}/${RETRO_ABI}")
    list(APPEND CMAKE_SYSTEM_PROGRAM_PATH "${RETRO_ROOT}/bin")
    set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
    set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
    set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
    set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)
endif()

set(RETRO ON)
if(RETRO_ABI MATCHES "^m68k-")
    set(CMAKE_SYSTEM_PROCESSOR m68k)
    set(RETRO_CARBON OFF CACHE BOOL "" FORCE)
    if(RETRO_ABI MATCHES "-palmos$")
        set(RETRO_PALMOS ON)
    else()
        set(RETRO_MAC ON)
        set(RETRO_68K ON)
    endif()
elseif(RETRO_ABI MATCHES "^powerpc-")
    set(CMAKE_SYSTEM_PROCESSOR powerpc)
    set(RETRO_MAC ON)
    if(RETRO_CARBON)
        set(CMAKE_EXE_LINKER_FLAGS_INIT -carbon)
        add_compile_definitions(TARGET_API_MAC_CARBON=1)
    else()
        set(RETRO_PPC ON)
    endif()
endif()
